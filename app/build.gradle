import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.dependency.license.report)
}

android {
    namespace 'com.richardluo.globalIconPack'
    compileSdk 36

    def versionProps = new Properties()
    versionProps.load(new FileInputStream("$project.rootDir/version.properties"))
    int major = versionProps['majorVersion'] as Integer
    int minor = versionProps['minorVersion'] as Integer
    int patch = versionProps['patchVersion'] as Integer

    defaultConfig {
        applicationId "com.richardluo.globalIconPack"
        minSdk 31
        targetSdk 36
        versionCode major * 10000 + minor * 100 + patch
        versionName "${major}.${minor}.${patch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def keystoreProps = new Properties()
    keystoreProps.load(new FileInputStream("$project.rootDir/keystore.properties"))
    signingConfigs {
        release {
            keyAlias keystoreProps['keyAlias']
            keyPassword keystoreProps['keyPassword']
            storeFile file(keystoreProps['storeFile'])
            storePassword keystoreProps['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debugApp {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }
    }

    buildFeatures {
        buildConfig true
        compose true
    }
    androidResources {
        generateLocaleConfig = true
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
}

tasks.register('printVersion') {
    doLast {
        println android.defaultConfig.versionName
    }
}

dependencies {
//    testImplementation libs.junit
//    androidTestImplementation libs.ext.junit
//    androidTestImplementation libs.espresso.core
    compileOnly libs.api
    implementation libs.core.ktx
    implementation libs.activity
    implementation libs.viewmodel
    implementation libs.compose.foundation
    implementation libs.preference
    implementation libs.material3
    implementation libs.material.icons.extended
    implementation libs.libsu
    implementation libs.documentfile
    implementation libs.hiddenapibypass
    implementation libs.navigation
    implementation libs.reorderable
}

import com.github.jk1.license.render.*

licenseReport {
    outputDir = project.layout.projectDirectory.dir("../licenses").getAsFile().path
    renderers = [new SimpleHtmlReportRenderer('third-party-libs.html')]
}
